BEGIN NEW DATA CASE
$INCLUDE, pv1_osg.prm
C  dT  >< Tmax >< Xopt >< Copt ><Epsiln>
   5.E-7     7.0                  1.E-12
   65535       5       0       0       1       0       0       1       0
/TACS
TACS HYBRID
98ISCN    =      8.2
98VOCN    =     36.8
98NS      =      60.
98KI      =   6.2E-4
98KV      = -0.00356
98A       =      1.3
98RP      =  1088.27
98RS      =    0.244
98MSER    =      15.
98MPAR    =       4.
98IPVN    =(RP+RS)*ISCN/RP
90G                                                                -1.      1.E3
90TEMP                                                             -1.      1.E3
98VPV     =ABS(DCP-DCN)/MSER
98ISWT    =RSEQ
98ITRM    =ISWT/MPAR
98DTEMP   =TEMP-25.0
98GPU     =G/1000.0
98AVT     =A*NS*(DTEMP+298.0)/11604.506
98I0      =(ISCN + KI*DTEMP) / (EXP((VOCN + KV*DTEMP)/AVT) - 1.0)
98IPV     =(IPVN + KI*DTEMP)*GPU
98ID      =I0*(EXP((VPV+RS*ITRM)/AVT) - 1.0)
98IM      =IPV - ID
98IPOS    =IM*MPAR
98INEG    =-IM*MPAR
90DCP                                                              -1.      1.E3
90DCN                                                              -1.      1.E3
91RSEQ                                                                      1.E3
23TX1             2..000833333.000416667                                    1.E3
88TRIANG65+TX2                                                                  
98TX2     =DELTAT*4800*(1-TX1)
98FIRE1   =M.GT.(TRIANG+1)
98FIRE2   =-M.GT.(TRIANG+1)
98FIRE3   =.NOT.FIRE1
98FIRE4   =.NOT.FIRE2
98MBASE   =      0.8
98M       =MBASE*(UD*COSTH - UQ*SINTH)
91ACP                                                                       1.E3
90ACN                                                                       1.E3
90LOAD                                                                      1.E3
98VMP     =    29.51
98IMP     =     7.63
98DIDT  59                +IPVPU                      1.
98IPVPU   = 0.0328 * RSEQ  
 0VPVPU           +DCP            -DCN            .00226                  
98IDEN  63+DENPAD                         +DIDT             1.
98DENPAD  =      0.1
98DELTV   =-DIDT*DELTAT*(TIMEX.GT.0.1)*VPVPU/IDEN
 0DBAR                    +DINTEG                     1.   .75   1.4
 1DRAW    +DELTV                                   -1.E4
        1.
                  1.
98DINTEG  = DRAW   + DINIT
98PPVPU   =IPVPU*VPVPU
90FCTRL                                                            -1.      1.E3
90UD                                                               -1.      1.E3
98ACVOLT  = LOAD   - ACN   
88KVEPS   =1.41421356*(PCCV - VP)
98QVEPS   = KVEPS  - QVP   
98DVP     = QVEPS  * WC    
 1VP      +DVP                                        1.
        1.
                  1.
 1X2      +VP                                         1.
        1.
                  1.
98QVP     = X2     * WC    
98WC      = 6.2832 * FCTRL 
88VRMS    =SQRT(VP*VP + QVP*QVP)/1.41421356
88COSTH   =COS(INTWC)
88SINTH   =SIN(INTWC)
88VOD     =VP*COSTH + QVP*SINTH
88VOQ     =-VP*SINTH + QVP*COSTH
88KIEPS   =1.41421356*(PCCI - IP)
98QIEPS   = KIEPS  - QIP   
98DIP     = QIEPS  * WC    
 1IP      +DIP                                        1.
        1.
                  1.
 1IX2     +IP                                         1.
        1.
                  1.
98QIP     = IX2    * WC    
88IRMS    =SQRT(IP*IP + QIP*QIP)/1.41421356
88IOD     =IP*COSTH + QIP*SINTH
88IOQ     =-IP*SINTH + QIP*COSTH
 1INTWC   +WC                                         1.
        1.
                  1.
90UQ                                                               -1.      1.E3
11DINIT       1.0433                                                        1.E3
 1PCCV    +ACVOLT                                     1.                  
        1.          
        1.     .0002
 1PCCI    +ACP                                        1.                  
        1.          
        1.     .0002
33DBAR  
33VRMS  
33VOD   
33VOQ   
33IRMS  
33IOD   
33IOQ   
33PPVPU 
77DBAR        1.0433
/MODELS
MODELS
MODEL DCDC           -- NB! 6 character name limit 

-- Start header. Do not modify the type-94 header. 
 comment---------------------------------------------------------------
 | First, declarations required for any type 94 Thevenin model         |
 | - these data and input values are provided to the model by ATP      |
 | - these output values are used by ATP                               |
 | - these names can be changed, except 'n', but not their order       |
 -------------------------------------------------------------endcomment

DATA  n                -- number of node pairs
      n2 {dflt: n*n}   -- number of matrix elements

INPUT vth[1..n]  -- Thev. voltage(t) across each branch
      rth[1..n2] -- Thev. resistance(t) matrix, symmetrical
      gth[1..n2] -- inverse of rth
      v0[1..n]   -- voltage(t=0) across each branch
      i0[1..n]   -- current(t=0) into each branch

VAR   v[1..n]    -- calculated voltage(t) across each branch
      i[1..n]    -- calculated current(t) into each branch

OUTPUT i[1..n]

 comment---------------------------------------------------------------
 | Next, declarations of user-defined data for this particular model   |
 | - their value is defined at the time of using the type-94 component |
 -------------------------------------------------------------endcomment
-- End header

VAR
  G1D -- D gain / Rthev1
  R11 -- Thevenin resistance from the PV side
  R22 -- Thevenin resistance from the inverter side

INIT
  v[1..3] := v0[1..3]
  i[1..3] := i0[1..3]
ENDINIT

EXEC
  R11 := rth[1]
  R22 := rth[5]
  v[3] := vth[3] -- D gain
  i[3] := 0.0
  G1D := v[3]*gth[1]
  COMBINE as GROUP1
    v[1] := sum(vth[1]| -rth[1]|i[1])
    v[2] := sum(vth[2]| -rth[5]|i[2])
    i[1] := sum(gth[1]|vth[1] -G1D|v[2])
    i[2] := sum(-v[3]|i[1])
  ENDCOMBINE
ENDEXEC
ENDMODEL
ENDMODELS
C        1         2         3         4         5         6         7         8
C 345678901234567890123456789012345678901234567890123456789012345678901234567890
/BRANCH
C < n1 >< n2 ><ref1><ref2>< R  >< L  >< C  >
C < n1 >< n2 ><ref1><ref2>< R  >< A  >< B  ><Leng><><>0
  DCP                                    .01                                   0
  DCN                                    .01                                   0
  DCP   DCN                             1.E4                                   2
  IPOS  RSEQ               .4575                                               0
  TB    TP                   0.0        0.05
  TP    IX0009                5.
  CFN   CFP                              20.                                   0
  TA    CFP                         1.                                         0
  TB    CFN                         1.                                         0
  TN    TB                   0.0        0.05
  TB    IX0010                5.
  TA    TP                   0.0        0.05
  TP    IX0011                5.
  TN    TA                   0.0        0.05
  TA    IX0012                5.
  CFP   ACP                         .2                                         0
  CFN   ACN                         .2                                         0
99IPOS  INEG                                                                   0
          0.1353            552.
            32.8            553.
            9999
        G                   1.E3                                               0
        TEMP                1.E3                                               0
  INEG  DCN                .4575                                               0
        TP                                .1                                   0
        TN                                .1                                   0
94DCDCLADCDCRADCDC  THEV                                                       0
94DCDCLBDCDCRB                                                                 0
94DCDCLCDCDCRC                                                                 0
 >SSV   DCDCLA
 >SSV   DCDCLB
 >SSV   DCDCLC
 >SSV   DCDCRA
 >SSV   DCDCRB
 >SSV   DCDCRC
 >SSI   
 >SSI   
 >SSI   
 >END
  DCDCRC                   .0001                                               0
  DCDCLCDBAR               .0001                                               0
  DCDCRADCN                  .01                                               0
  DCDCLADCP                  .01                                               0
  TN    DCDCRB               .01                                               0
  DCDCLBTP                   .01                                               0
97LOAD  ACN                                                                    2
             0.0          RLOD1_
            3.00          RLOD2_
            9999
        FCTRL               1.E3                                               0
        UD                  1.E3                                               0
        UQ                  1.E3                                               0
/SWITCH
C < n 1>< n 2>< Tclose ><Top/Tde ><   Ie   ><Vf/CLOP ><  type  >
  RSEQ  DCP                                           MEASURING                1
11TB    IX0009                                                                 0
  ACP   LOAD                                          MEASURING                1
13TA    TP                                                  GIFU      FIRE1    0
13TB    TP                                                  GIFU      FIRE2    0
13TN    TB                                                  GIFU      FIRE4    0
13TN    TA                                                  GIFU      FIRE3    0
11TN    IX0010                                                                 0
11TA    IX0011                                                                 0
11TN    IX0012                                                                 0
/SOURCE
C < n 1><>< Ampl.  >< Freq.  ><Phase/T0><   A1   ><   T1   >< TSTART >< TSTOP  >
60IPOS  -1                                                         -1.      1.E3
60INEG  -1                                                         -1.      1.E3
12G      0G1________                  .1                          1.00      1.E3
12G      0DG________                  .1                          4.00      1.E3
12TEMP   0DTEMP_____                  .1                          4.00      1.E3
11TEMP   0TEMP0_____                                               -1.      1.E3
60DBAR   0                                                                  1.E3
12FCTRL  0DFC_______                .001                          4.00      1.E3
11FCTRL  0FC0_______                                               -1.      1.E3
12UD     0DUD_______                .001                          4.00      1.E3
11UD     0UD0_______                                               -1.      1.E3
12UQ     0DUQ_______                .001                          4.00      1.E3
11UQ     0UQ0_______                                               -1.      1.E3
/OUTPUT
  G     TEMP  FCTRL UD    UQ    
BLANK TACS
BLANK MODELS
BLANK BRANCH
BLANK SWITCH
BLANK SOURCE
BLANK OUTPUT
BLANK PLOT
BEGIN NEW DATA CASE
BLANK
